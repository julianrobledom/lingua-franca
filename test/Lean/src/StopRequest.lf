// Tests whether stop request work properly. 
// This is the case if requesting a stop terminates execution upon completion
// of the next microstep.
//
// Note: The validity of this test relies on logical time being zero-based,
//       the correctness of the `getTag` operation and the scheduling of 
//       actions working properly.
//
// Note: There's many aspects of stop requests which this test does not test.
target Lean

reactor Test { 
    // We repeatedly schedule this action without delay, so that we give the
    // reaction below every chance to trigger after the tag which should
    // actually perform the final instantaneous execution.
    logical action a : Unit

    reaction(startup, a) {=
        // As we request a stop immediately, the final execution of this
        // reaction should occur at tag (0,1). So if we see a tag greater
        // than that the stop request has failed.
        assert! (← getTag) ≤ ⟨0, 1⟩

        requestStop
        schedule .a 0 ()
    =}
}

main reactor {
    t = new Test()
}